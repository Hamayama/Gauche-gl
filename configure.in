dnl
dnl Configuring gauche-gl 
dnl  process this file with "autoconf -l `gauche-config --ac`" to
dnl  generate 'configure'.
dnl $Id: configure.in,v 1.14 2003-10-05 05:54:48 shirok Exp $
AC_PREREQ(2.54)
AC_INIT(Gauche-gl, 0.2.3, shiro@acm.org)
AC_GAUCHE_INIT_EXT
AC_CANONICAL_SYSTEM

AC_PATH_XTRA

case "$target" in
  *-*-cygwin*)
    LIBGLUT=glut32;;
  *)
    LIBGLUT=glut;;
esac

# options for configure scripts
GLUT_LIB="-l$LIBGLUT"
AC_ARG_WITH(glut, [  --with-glut[=PATH]:      Use GLUT library installed under PATH.
                          By default, Gauche-gl assumes GLUT is installed in
                          the system standard location.  If you do not want to
                          use GLUT support, specify --with-glut=no.], [
  case $with_glut in
    no)  GLUT_LIB= ;;
    yes) GLUT_LIB="-l$LIBGLUT";;
    *)   GLUT_LIB="-l$LIBGLUT"
         GLUT_INCDIR="-I$with_glut/include"
         GLUT_LIBDIR="-L$with_glut/lib";;
  esac
])

# Check for headers
AC_CHECK_HEADERS(GL/glext.h, , , GL/gl.h)
AC_CHECK_HEADERS(GL/glx.h, [   # OpenGL/X11
     # NB: FreeBSD seems to need -lX11 -lXext.  We are on X11 system anyway,
     # so I assume this won't do any harm.
     GL_LIBS='-lGLU -lGL -lX11 -lXext'
     GLUT_LIB="$GLUT_LIB -lXmu -lXi"
], ,)
AC_CHECK_HEADERS(GLUT/glut.h, [
      case $target in
      powerpc-apple-darwin*)  
          AC_DEFINE(MacOSX, 1) # darwin which has GLUT/glut.h is MacOSX
          GL_LIBS='-framework OpenGL'
          GLUT_LIB='-framework GLUT' ;;
      *)  # other OSes w/o X11 but have GLUT/glut.h. Possibly MacOS9??
      ;;
      esac  
], ,)

if test "$GL_LIBS" = ""; then
  case $target in
    *-*-cygwin*)
      GL_LIBS='-lglu32 -lopengl32' ;;
    *)
      GL_LIBS='-lGLU -lGL' ;;
  esac
fi

AC_SUBST(GL_LIBS)
AC_SUBST(GLUT_LIB)
AC_SUBST(GLUT_INCDIR)
AC_SUBST(GLUT_LIBDIR)

# Sets the default value of INSTALL_TYPE macro.
AC_GAUCHE_INSTALL_TYPE(sys)

# Get compiler parameters which Gauche has been compiled with.
AC_GAUCHE_CC
AC_GAUCHE_FLAGS

# Check for other programs.
AC_PROG_INSTALL
AC_CHECK_PROGS(MAKEINFO, makeinfo)
AC_CHECK_PROGS(GZIP_PROGRAM, gzip)

# Check for libraries

# on cygwin, we need several extra -l flags.
case "$target" in
  *-*-cygwin*)
    LIBS="-lgauche-uvector $LIBS"
    GL_EXTRALIBS="-L. -lgauche-math3d"
    GLUT_EXTRALIBS="-L. -lgauche-gl -lgauche-math3d"
    ;;
esac
AC_SUBST(GL_EXTRALIBS)
AC_SUBST(GLUT_EXTRALIBS)

# Set LDFLAGS to generate shared library.
AC_GAUCHE_FIX_LIBS

# Output
AC_GAUCHE_EXT_FIXUP(src/gl, gauche_gl)
AC_GAUCHE_EXT_FIXUP(src/glut, gauche_glut)
AC_GAUCHE_EXT_FIXUP(src/math3d, gauche_math3d)
echo $PACKAGE_VERSION > VERSION
AC_OUTPUT(Makefile src/Makefile lib/Makefile doc/Makefile src/extralibs.sh)
