dnl
dnl Configuring gauche-gl 
dnl  process this file with "autoconf -l `gauche-config --ac`" to
dnl  generate 'configure'.
dnl $Id: configure.in,v 1.9 2002-09-27 05:09:26 shirok Exp $
AC_INIT(src/gl-lib.stub)
AC_GAUCHE_INIT_EXT
AC_CANONICAL_SYSTEM

AC_PATH_XTRA

# options for configure scripts
GLUT_LIB="-lglut"
AC_ARG_WITH(glut, [  --with-glut[=PATH]:      Use GLUT library installed under PATH.
                          By default, Gauche-gl assumes GLUT is installed in
                          the system standard location.  If you do not want to
                          use GLUT support, specify --with-glut=no.], [
  case $with_glut in
    no)  GLUT_LIB= ;;
    yes) ;;
    *)   GLUT_LIB="-lglut";
         GLUT_INCDIR="-I$with_glut/include";
         GLUT_LIBDIR="-L$with_glut/lib";;
  esac
])

# Check for headers
AC_CHECK_HEADERS(GL/glext.h, , , GL/gl.h)
AC_CHECK_HEADERS(GL/glx.h, [   # OpenGL/X11
     GL_LIBS='-lGLU -lGL'
     GLUT_LIB="$GLUT_LIB -lXmu -lXi"
], ,)
AC_CHECK_HEADERS(GLUT/glut.h, [
      case $host in
      powerpc-apple-darwin*)  
          AC_DEFINE(MacOSX, 1) # darwin which has GLUT/glut.h is MacOSX
          GL_LIBS='-framework OpenGL'
          GLUT_LIB='-framework GLUT' ;;
      *)  # other OSes w/o X11 but have GLUT/glut.h. Possibly MacOS9??
      ;;
      esac  
], ,)

AC_SUBST(GL_LIBS)
AC_SUBST(GLUT_LIB)
AC_SUBST(GLUT_INCDIR)
AC_SUBST(GLUT_LIBDIR)

# Sets the default value of INSTALL_TYPE macro.
AC_GAUCHE_INSTALL_TYPE(sys)

# Get compiler parameters which Gauche has been compiled with.
AC_GAUCHE_CC
AC_GAUCHE_FLAGS



# Check for other programs.
AC_PROG_INSTALL

# Check for libraries

# Set LDFLAGS to generate shared library.
AC_GAUCHE_FIX_LIBS

# Output
AC_GAUCHE_EXT_FIXUP(src/gl, gauche_gl)
AC_GAUCHE_EXT_FIXUP(src/glut, gauche_glut)
AC_GAUCHE_EXT_FIXUP(src/math3d, gauche_math3d)
AC_OUTPUT(Makefile src/Makefile lib/Makefile)
