dnl
dnl Configuring gauche-gl 
dnl  process this file with "autoconf -l `gauche-config --ac`" to
dnl  generate 'configure'.
dnl $Id: configure.in,v 1.6 2002-02-15 10:16:04 shirok Exp $
AC_INIT(src/gl-lib.stub)
AC_GAUCHE_INIT_EXT

# options for configure scripts
GLUT_LIB="-lglut"
AC_ARG_WITH(glut, [  --with-glut[=PATH]:      Use GLUT library installed under PATH.
                          By default, Gauche-gl assumes GLUT is installed in
                          the system standard location.  If you do not want to
                          use GLUT support, specify --with-glut=no.], [
  case $with_glut in
    no)  GLUT_LIB= ;;
    yes) ;;
    *)   GLUT_LIB="-lglut";
         GLUT_INCDIR="-I$with_glut/include";
         GLUT_LIBDIR="-L$with_glut/lib";;
  esac
])

AC_SUBST(GLUT_LIB)
AC_SUBST(GLUT_INCDIR)
AC_SUBST(GLUT_LIBDIR)

# Sets the default value of INSTALL_TYPE macro.
AC_GAUCHE_INSTALL_TYPE(sys)

# Get compiler parameters which Gauche has been compiled with.
AC_GAUCHE_CC
AC_GAUCHE_FLAGS

# Check for other programs.
AC_PROG_INSTALL

# Check for libraries
AC_PATH_XTRA

# Set LDFLAGS to generate shared library.
AC_GAUCHE_FIX_LIBS

# Output
AC_GAUCHE_EXT_FIXUP(src/gl, gauche_gl)
AC_GAUCHE_EXT_FIXUP(src/glut, gauche_glut)
AC_OUTPUT(Makefile src/Makefile lib/Makefile)
