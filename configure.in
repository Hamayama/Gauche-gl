dnl
dnl Example of configuring Gauche extension
dnl
dnl $Id: configure.in,v 1.3 2001-10-10 08:40:21 shirok Exp $
AC_INIT(src/gl-lib.stub)

dnl Determine if we're configuring inside of Gauche main source tree or not.
if test -f ../../src/gauche.h; then
  GAUCHE_CONFIG="sh ../../src/gauche-config"
  GAUCHE_TOP='../../'
  GAUCHE_INC="-I../../src -I../../gc"
  GOSH="../../src/gosh -I../../src -I../../lib"
else
  GAUCHE_CONFIG=gauche-config
  GAUCHE_TOP=
  GAUCHE_INC=`gauche-config -I`
  GOSH=gosh
fi
AC_SUBST(GAUCHE_CONFIG)
AC_SUBST(GAUCHE_TOP)
AC_SUBST(GAUCHE_INC)
AC_SUBST(GOSH)

dnl options for configure scripts
GLUT_LIB="-lglut"
AC_ARG_WITH(glut, [  --with-glut[=PATH]:      Use GLUT library installed under PATH.
                          By default, Gauche-gl assumes GLUT is installed in
                          the system standard location.  If you do not want to
                          use GLUT support, specify --with-glut=no.], [
  case $with_glut in
    no)  GLUT_LIB= ;;
    yes) ;;
    *)   GLUT_LIB="-lglut";
         GLUT_INCDIR="-I$with_glut/include";
         GLUT_LIBDIR="-L$with_glut/lib";;
  esac
])

AC_SUBST(GLUT_LIB)
AC_SUBST(GLUT_INCDIR)
AC_SUBST(GLUT_LIBDIR)

dnl A user has a choice to install the module into Gauche system directory
dnl or site local directory, by setting environment variable INSTALL_TYPE
dnl to either 'sys' or 'site'.   The variable can be overridden at make time.
: ${INSTALL_TYPE=sys}  dnl This sets the default.
AC_SUBST(INSTALL_TYPE)

dnl Get compiler parameters which Gauche has been compiled with.
CC="`$GAUCHE_CONFIG --cc`"
AC_SUBST(CC)
CFLAGS="$CFLAGS $GAUCHE_INC `$GAUCHE_CONFIG --so-cflags`"
AC_SUBST(CFLAGS)

dnl Check for other programs.
AC_PROG_INSTALL

dnl Check for libraries
AC_PATH_XTRA

dnl Set LDFLAGS to generate shared library.
dnl This has to come after all the tests that requre linking, or those test
dnl will fail because they can't generate stand-alone executable.
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --so-ldflags`"
AC_SUBST(LDFLAGS)

dnl Output
AC_OUTPUT(Makefile src/Makefile)
